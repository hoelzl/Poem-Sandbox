#lang poem

(provide <result-strategy>
	 collect-result
	 <all-results-strategy> <one-result-strategy>
	 <n-results-strategy>
	 record-failure!
	 number-of-results number-of-results-so-far
	 <numeric-results-strategy>
	 
	 <search-strategy>
	 next-node! add-node! add-nodes!
	 <list-search-strategy> <dfs> <bfs>
	 <ids-node> 
	 ids-node-depth set-ids-node-depth!
	 ids-node-value set-ids-node-value!

	 <ids>
	 ;;; Not sure whether these should be exported here
	 ;;; Maybe put them into a submodule for testing?
	 nodes set-nodes!
	 initial-nodes set-initial-nodes!
	 max-depth set-max-depth!
	 curr-depth set-curr-depth!
	 need-deepening? set-need-deepening?!

	 collect
	 amb amb* deliver fail
	 amb-list amb-list*

	 an-integer->= an-integer-<= 
	 an-integer-between an-integer-between* an-integer-between**
	 a-multiple-of a-factor-of
	 an-element-of)

(require racket/control)
(require data/queue)

(define debugging? #f)
(define (debug-printf . args)
  (when debugging?
    (apply printf args)))


;;; Result Strategies
;;; =================

(defclass <result-strategy> ())

;; Collect a result and returns true, if the computation should
;; continue, falso otherwise.
(defgeneric collect-result (strategy node))

;; Notification that `fail' was called.  Currently mostly useful for
;; tracing and debugging, but also intended for reinforcement
;; learning.
(defgeneric record-failure! (strategy))
(defmethod record-failure! (strategy)
  ;; Do nothing.
  #f)

;;; Return the results collected by the strategy as a list.
(defgeneric result-list (strategy))

(defclass <multi-result-strategy> (<result-strategy>)
  (results :initializer (thunk (make-queue)))
  :autoaccessors :slot
  :auto #t)

(defmethod collect-result ((strategy <multi-result-strategy>) node)
  (define node-is-void? (eq? node (void)))
  (unless node-is-void?
    (enqueue! (results strategy) node))
  #t)

(defmethod result-list ((strategy <multi-result-strategy>))
  (queue->list (results strategy)))


;;; Noting to implement, since the inherited default is already
;;; sufficient.
(defclass <all-results-strategy> (<multi-result-strategy>))


(defclass <one-result-strategy> (<result-strategy>)
  (result)
  :autoaccessors :slot
  :auto #t)

(defmethod collect-result ([strategy <one-result-strategy>] node)
  (define node-is-void? (eq? node (void)))
  (cond [node-is-void?
	 #t]
	[else
	 (setf! (result strategy) node)
	 #f]))

(defmethod result-list ([strategy <one-result-strategy>])
  (list (result strategy)))

(defclass <n-results-strategy> (<multi-result-strategy>)
  (number-of-results :initvalue 1)
  (number-of-results-so-far :initvalue 0)
  :auto #t
  :autoaccessors :slot)

(defmethod collect-result ((strategy <n-results-strategy>) node)
  (cond [(eq? node (void))
	 #t]
	[else
	 (inc! (number-of-results-so-far strategy))
	 (enqueue! (results strategy) node)
	 (< (number-of-results-so-far strategy)
	    (number-of-results strategy))]))


(defclass <numeric-results-strategy> (<multi-result-strategy>))

(defmethod collect-result ((strategy <numeric-results-strategy>) node)
  (when (number? node)
    (enqueue! (results strategy) node))
  #t)


(defclass <search-strategy> ()
  (result-strategy :initializer (thunk (make <all-results-strategy>)))
  :auto #t
  :autoaccessors :slot)

(defgeneric next-node! ((strategy <search-strategy>)))

(defgeneric add-initial-node! ((strategy <search-strategy>) node))
(defmethod add-initial-node! ((strategy <search-strategy>) node)
  (add-node! strategy node))
;;; Do we need add initial-nodes! as well?

(defgeneric add-node! ((strategy <search-strategy>) node))
(defgeneric add-nodes! ((strategy <search-strategy>) nodes))

(defmethod collect-result ((strategy <search-strategy>) node)
  (collect-result (result-strategy strategy) node))

(defmethod result-list ([strategy <search-strategy>])
  (result-list (result-strategy strategy)))

(defclass <list-search-strategy> (<search-strategy>)
  (nodes :initvalue '())
  :auto #t
  :autoaccessors :slot)

(defmethod next-node! ((strategy <list-search-strategy>))
  (define ks (nodes strategy))
  (if (empty? ks)
      #f
      (begin
	(setf! (nodes strategy) (rest ks))
	(first ks))))


(defclass <dfs> (<list-search-strategy>))

(defmethod add-node! ((strategy <dfs>) k)
  (setf! (nodes strategy)
	 (list* k (nodes strategy))))

(defmethod add-nodes! ((strategy <dfs>) ks)
  (setf! (nodes strategy)
	 (append ks (nodes strategy))))


(defclass <bfs> (<list-search-strategy>))

(defmethod add-node! ((strategy <bfs>) k)
  (setf! (nodes strategy)
	 (append (nodes strategy) (list k))))

(defmethod add-nodes! ((strategy <bfs>) ks)
  (setf! (nodes strategy)
	 (append (nodes strategy) ks)))

(defentityclass <ids-node> ()
  depth
  value
  :auto #t
  :autoaccessors :class-slot)

(defaftermethod initialize ((node <ids-node>) initargs)
  (set-instance-proc! node (ids-node-value node)))

(defaftermethod set-ids-node-value! ((node <ids-node>) value)
  (set-instance-proc! node value))

(defclass <ids> (<search-strategy>)
  (nodes :initvalue '())
  (initial-nodes :initvalue #f)
  (max-depth :initvalue 1)
  (curr-depth :initvalue 0)
  (need-deepening? :initvalue #f)
  :auto #t
  :autoaccessors :slot)

(defmethod collect-result ((strategy <ids>) result)
  (debug-printf "collect-result: ~a\n" result)
  (debug-printf "max-depth: ~a, curr-depth: ~a\n" 
		(max-depth strategy) (curr-depth strategy))
  (if (= (max-depth strategy) (curr-depth strategy))
      (collect-result (result-strategy strategy) result)
      #t))

(defmethod next-node! ((strategy <ids>))
  (define ns (nodes strategy))
  (cond ((empty? ns)
	 (cond ((need-deepening? strategy)
		(unless (initial-nodes strategy)
		  (error "Iterative deepening without initial nodes?"))
		(setf! (need-deepening? strategy) #f)
		(inc! (max-depth strategy))
		(setf! (nodes strategy) (rest (initial-nodes strategy)))
		(let ((node (first (initial-nodes strategy))))
		  (setf! (curr-depth strategy) (ids-node-depth node))
		  node))
	       (else #f)))
	(else
	 (setf! (nodes strategy) (rest ns))
	 (let ((node (first ns)))
	   (setf! (curr-depth strategy) (ids-node-depth node))
	   node))))

(define (maybe-make-ids-node strategy val)
  (if ((curr-depth strategy) . >= . (max-depth strategy))
      (begin (setf! (need-deepening? strategy) #t) 
	     #f)
      (make <ids-node>
	:depth (add1 (curr-depth strategy))
	:value val)))

(define (maybe-make-ids-nodes strategy vals)
  (if ((curr-depth strategy) . >= . (max-depth strategy))
      (begin (setf! (need-deepening? strategy) #t) 
	     #f)
      (map (lambda (val)
	     (make <ids-node>
	       :depth (add1 (curr-depth strategy))
	       :value val))
	   vals)))

(defmethod add-node! ((strategy <ids>) k)
  (let ((new-node (maybe-make-ids-node strategy k)))
    (when new-node
      (unless (initial-nodes strategy)
	(setf! (initial-nodes strategy) (list new-node)))
      (setf! (nodes strategy)
	     (list* new-node (nodes strategy))))))

(defmethod add-nodes! ((strategy <ids>) ns)
  ;; We suppose that the new nodes are all at the same depth
  (let ((new-nodes (maybe-make-ids-nodes strategy ns)))
    (when new-nodes
      (unless (initial-nodes strategy)
	(setf! (initial-nodes strategy) new-nodes))
      (setf! (nodes strategy)
	     (append new-nodes (nodes strategy))))))


;;; Amb and friends

(define amb-tag (make-continuation-prompt-tag 'amb))
(define amb-abort-tag (make-continuation-prompt-tag 'amb-abort))
(define amb-search-strategy (make-parameter #f))

(define-syntax amb
  (syntax-rules ()
    [(_ value ...)
     (call-with-current-continuation
      (lambda (cont)
	(debug-printf "amb: adding nodes\n")
	(add-nodes!
	 (amb-search-strategy)
	 (list (thunk
		 (debug-printf "Calling cont\n")
		 (cont value)
		 (debug-printf "Should never get here\n")) ...))
	(debug-printf "amb: failing: strategy ~a\n" (amb-search-strategy))
	(fail)
	#;(error "Amb fell through?")
	)
      amb-tag)]))


(define (amb-list lst)
  (call-with-current-continuation
   (lambda (cont)
     (debug-printf "amb-list: adding nodes\n")
     (add-nodes!
      (amb-search-strategy)
      (map (lambda (val)
	     (thunk
	      (debug-printf "Calling value cont\n")
	      (cont (val))
	      (debug-printf "Should never get here\n")))
	   lst))
     (debug-printf "amb-list: failing: strategy ~a\n"
		   (amb-search-strategy))
     (fail))
   amb-tag))

;;; Should behave identical to amb
(define-syntax amb*
  (syntax-rules ()
    [(_ value ...)
     (amb-list* (list (thunk value) ...))]))


;;; Similar to amb-list, but does not expect thunks as arguments.
;;; This behaves differently when backtracking!
(define (amb-list* lst)
  (call-with-current-continuation
   (lambda (cont)
     (debug-printf "amb-list: adding nodes\n")
     (add-nodes!
      (amb-search-strategy)
      (map (lambda (val)
	     (thunk
	      (debug-printf "Calling value cont\n")
	      (cont val)
	      (debug-printf "Should never get here\n")))
	   lst))
     (debug-printf "amb-list: failing: strategy ~a\n"
		   (amb-search-strategy))
     (fail))
   amb-tag))

(define (deliver value)
  (debug-printf "Delivering ~a\n" value)
  (call-with-current-continuation
   (lambda (cont)
     (define strategy (amb-search-strategy))
     (add-node!
      strategy
      (thunk
	(debug-printf "Continuing after delivering a value\n")
	(cont 'command-continuation)))
     (add-node!
      strategy
      (thunk
       (debug-printf "Delivering value ~a\n" value)
       (abort/cc amb-tag 'deliver value)))
     (fail)
     #;(error "Deliver fell through?")
     )
   amb-tag)
  (void))

(define (fail)
  (debug-printf "Failing\n")
  (define strategy (amb-search-strategy))
  (define cc (next-node! strategy))
  (record-failure! strategy)
  (if cc
      (abort/cc amb-tag 'cont cc)
      (abort/cc amb-abort-tag)))

(define-syntax collect
  (syntax-rules ()
    [(_ (search-strategy) body ...)
     (let ()
       (define (process-result res)
	 (define continue? (collect-result (amb-search-strategy) res))
	 (cond [continue?
		(debug-printf "Continuing\n")
		(call-with-continuation-prompt 
		 fail amb-tag amb-handler)]
	       [else
		(abort/cc amb-abort-tag)]))
       (define (amb-handler kind value)
	 (case kind
	   [(cont)
	    (debug-printf "In collect/cont 1: ~a\n" value)
	    (let ([res (call-with-continuation-prompt 
			value amb-tag amb-handler)])
	      (process-result res))]
	   [(deliver)
	    (debug-printf "In collect/deliver: ~a\n" value)
	    (process-result value)]))
       (parameterize ([amb-search-strategy search-strategy])
	 (call-with-continuation-prompt
	  (thunk
	   (call-with-continuation-prompt
	    (thunk
	     (call-with-current-continuation
	      (lambda (cont)
		(add-initial-node!
		 (amb-search-strategy)
		 (thunk
		  (debug-printf "Returning Initial value\n")
		  (abort/cc amb-tag 'deliver 
			    ;; Cannot use `begin' here, since this
			    ;; results in an error when body...
			    ;; starts with a definition
			    (let () body ...))))
		(fail))
	      amb-tag)) amb-tag amb-handler))
	  amb-abort-tag
	  (thunk (result-list (amb-search-strategy))))
	 (result-list (amb-search-strategy))))]
    [(_ () body ...)
     (collect ((make <dfs>)) body ...)]))


(define (an-integer->= n)
  (amb n (an-integer->= (+ n 1))))

(define (an-integer-<= n)
  (amb n (an-integer-<= (- n 1))))

(define (an-integer-between m n)
  (amb-list*
   (for/list ([i (in-range m (add1 n))])
     i)))

(define-for-syntax (make-integer-list m n)
  (if (m . > . n)
      '()
      (cons m (make-integer-list (add1 m) n))))

(define-syntax (an-integer-between* stx)
  (syntax-case stx ()
    [(_ m n)
     #`(amb #,@(make-integer-list (syntax->datum #'m) (syntax->datum #'n)))]))

(define (an-integer-between** m n)
  (if (> m n)
      (fail)
      (amb m (an-integer-between (+ m 1) n))))

;; (an-integer-between* 1 10)

(define (a-multiple-of k)
  (let ([multiplier (an-integer->= 1)])
    (* multiplier k)))

(define (a-factor-of** k)
  (let ([i (an-integer-between 2 (floor (/ k 2)))])
    (if (zero? (modulo k i))
	i
	(fail))))

(define (a-factor-of* k)
  (let loop ([i 2])
    (cond [(zero? (modulo k i))
	   (deliver i)
	   (loop (add1 i))]
	  [(i . >= . (floor (/ k 2)))
	   (fail)]
	  [else
	   (loop (add1 i))])))

(define (a-factor-of k)
  (for ([i (in-range 2 (floor (/ k 2)))])
    (when (zero? (modulo k i))
      (deliver i)))
  (fail))

(define (an-element-of lst)
  (if (null? lst)
      (fail)
      (amb (car lst) (an-element-of (cdr lst)))))
